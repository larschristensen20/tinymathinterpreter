grammar dk.sdu.mdsd.Mathinterpreter with org.eclipse.xtext.common.Terminals

generate mathinterpreter "http://www.sdu.dk/mdsd/Mathinterpreter"

MathExp:
	'exp' exp=Exp
;

Exp:
	PlusOrMinus
;

Atomic returns Exp:
	{Number} value=INT
;

Primary returns Exp:
	'(' Exp ')' |
	Atomic
;

PlusOrMinus returns Exp:
	MultOrDiv (
		({Plus.left=current} op='+' | {Minus.left=current} op='-') 
			right=MultOrDiv
		)*
;

MultOrDiv returns Exp:
	Primary (
		({Mult.left=current} op='*' | {Div.left=current} op='/') 
			right=Primary
		)*
;